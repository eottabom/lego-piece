plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'checkstyle'
	id 'idea'
	id 'com.google.protobuf' version '0.8.19'
}

group = 'lego'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = "1.56.0"
	protobufVersion = '4.28.2'
	protocolBufferVersion = '3.25.0'
	reactiveGrpcVersion = '1.2.4'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	implementation("io.grpc:grpc-netty-shaded:$grpcVersion") // gRPC 서버를 위한 Netty 전송
	implementation("io.grpc:grpc-protobuf:$grpcVersion") // gRPC 의 프로토콜 버퍼 지원
	implementation("io.grpc:grpc-stub:$grpcVersion") // 클라이언트 stub 생성 지원
	implementation("com.google.protobuf:protobuf-java:$protobufVersion") // Protobuf Java 지원

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	runtimeOnly 'com.h2database:h2'

	checkstyle 'io.spring.javaformat:spring-javaformat-checkstyle:0.0.40'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

checkstyle {
	configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
	configProperties.suppressionsFile = file("$rootDir/config/checkstyle/suppressions.xml")
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protocolBufferVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			// Configure the task - apply codegen plugins.
			grpc {}
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

